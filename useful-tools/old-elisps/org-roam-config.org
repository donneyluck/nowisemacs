* org-roam
** org-roam
#+begin_src elisp :tangle no
  (setup emacsql-sqlite-builtin)
  (setup org-roam
    (:option org-roam-database-connector 'sqlite-builtin
             org-roam-mode-sections       '(org-roam-backlinks-section
                                            org-roam-reflinks-section
                                            ;; org-roam-unlinked-references-section
                                            )
             org-roam-directory (concat nowisemacs-doc-root-dir "/orgmode/roam/")
             org-roam-dailies-directory (concat nowisemacs-doc-root-dir "/orgmode/roam/dailies")

             org-roam-db-gc-threshold most-positive-fixnum
             ;; org-roam-node-display-template "${doom-hierarchy:*} ${tags:37}"
             org-roam-node-display-template (concat "${namespace:15} ${doom-hierarchy:80} "
                                                    (propertize "${tags:*}" 'face 'org-tag))

             org-roam-capture-templates '(("m" "main" plain "%?"
                                           :target
                                           (file+head "main/%<%Y-%m-%d>-${slug}.org"
                                                      "#+title: ${title}\n* Action notes\n* Idea notes\n* Sealed notes")
                                           :unnarrowed t)
                                          ("r" "reference" plain "%?"
                                           :target
                                           (file+head "reference/%<%Y-%m-%d>-${slug}.org"
                                                      "#+title: ${title}\n* Action notes\n* Idea notes\n* Sealed notes")
                                           :unnarrowed t)
                                          ("a" "article" plain "%?"
                                           ;; :target
                                           :if-new
                                           (file+head "articles/%<%Y-%m-%d>-${slug}.org"
                                                      "#+title: ${title}\n* Action notes\n* Idea notes\n* Sealed notes")
                                           :unnarrowed t))
             )
    (:when-loaded
      (org-roam-db-autosync-mode)

      ;; Codes blow are used to general a hierachy for title nodes that under a file
      (cl-defmethod org-roam-node-doom-filetitle ((node org-roam-node))
        "Return the value of \"#+title:\" (if any) from file that NODE resides in.
       If there's no file-level title in the file, return empty string."
        (or (if (= (org-roam-node-level node) 0)
                (org-roam-node-title node)
              (org-roam-get-keyword "TITLE" (org-roam-node-file node)))
            ""))
      (cl-defmethod org-roam-node-doom-hierarchy ((node org-roam-node))
        "Return hierarchy for NODE, constructed of its file title, OLP and direct title.
         If some elements are missing, they will be stripped out."
        (let ((title     (org-roam-node-title node))
              (olp       (org-roam-node-olp   node))
              (level     (org-roam-node-level node))
              (filetitle (org-roam-node-doom-filetitle node))
              (separator (propertize " > " 'face 'shadow)))
          (cl-case level
            ;; node is a top-level file
            (0 filetitle)
            ;; node is a level 1 heading
            (1 (concat (propertize filetitle 'face '(shadow italic))
                       separator title))
            ;; node is a heading with an arbitrary outline path
            (t (concat (propertize filetitle 'face '(shadow italic))
                       separator (propertize (string-join olp " > ") 'face '(shadow italic))
                       separator title)))))


      (cl-defmethod org-roam-node-namespace ((node org-roam-node))
        "Return the TYPE of NODE."
        (condition-case nil
            (file-name-nondirectory
             (directory-file-name
              (file-name-directory
               (file-relative-name (org-roam-node-file node) org-roam-directory))))
          (error "")))
      (add-to-list 'display-buffer-alist
             '("\\*org-roam\\*"
               (display-buffer-in-direction)
               (direction . right)
               (window-width . 0.33)
               (window-height . fit-window-to-buffer)))
      (org-roam-buffer-toggle)
      ;; (magit-section-show-level-2-all)

      ;; Every Zettel is a Draft until Declared Otherwise
      (add-hook 'org-roam-capture-new-node-hook (lambda()
                                                  (org-roam-tag-add '("draft"))))
      ))
#+end_src

** org-roam-ui
#+begin_src elisp :tangle no
    (setup websocket)
    (setup simple-httpd)
    (setup org-roam-ui
      (:option org-roam-ui-browser-function #'eaf-open-browser-other-window))
#+end_src
** consult-org-roam
#+begin_src elisp :tangle no
  (setup consult-org-roam
    (:hook-into org-roam-mode))
#+end_src
** org-roam-agenda
The main purpose for this section is to add every node that has a "TODO" into org-agenda, so I can see all the "TODO"s
among all the files in org-roam in a single agenda-view. The code was borrowed from vulpea and now in a repo "org-roam-agenda". You can see in [[https://github.com/nowislewis/org-roam-agenda.git][org-roam-agenda]]
#+begin_src elisp :tangle no
;; -*- lexical-binding: t; -*-

(defun vulpea-buffer-p ()
  "Return non-nil if the currently visited buffer is a note."
  (and buffer-file-name
       (string-prefix-p
        (expand-file-name (file-name-as-directory org-roam-directory))
        (file-name-directory buffer-file-name))))

(defun vulpea-project-files ()
  "Return a list of note files containing 'project' tag." ;
  (seq-uniq
   (seq-map
    #'car
    (org-roam-db-query
     [:select [nodes:file]
              :from tags
              :left-join nodes
              :on (= tags:node-id nodes:id)
              :where (like tag (quote "%\"project\"%"))]))))

(defun vulpea-agenda-files-update (&rest _)
  "Update the value of `org-agenda-files'."
  (setq org-agenda-files (vulpea-project-files)))

(add-hook 'find-file-hook #'vulpea-project-update-tag)
(add-hook 'before-save-hook #'vulpea-project-update-tag)

(advice-add 'org-agenda :before #'vulpea-agenda-files-update)
(advice-add 'org-todo-list :before #'vulpea-agenda-files-update)

;; functions borrowed from `vulpea' library
;; https://github.com/d12frosted/vulpea/blob/6a735c34f1f64e1f70da77989e9ce8da7864e5ff/vulpea-buffer.el
(defun vulpea-buffer-tags-get ()
  "Return filetags value in current buffer."
  (vulpea-buffer-prop-get-list "filetags" "[ :]"))

(defun vulpea-buffer-tags-set (&rest tags)
  "Set TAGS in current buffer.

  If filetags value is already set, replace it."
  (if tags
      (vulpea-buffer-prop-set
       "filetags" (concat ":" (string-join tags ":") ":"))
    (vulpea-buffer-prop-remove "filetags")))

(defun vulpea-buffer-tags-add (tag)
  "Add a TAG to filetags in current buffer."
  (let* ((tags (vulpea-buffer-tags-get))
         (tags (append tags (list tag))))
    (apply #'vulpea-buffer-tags-set tags)))

(defun vulpea-buffer-tags-remove (tag)
  "Remove a TAG from filetags in current buffer."
  (let* ((tags (vulpea-buffer-tags-get))
         (tags (delete tag tags)))
    (apply #'vulpea-buffer-tags-set tags)))

(defun vulpea-buffer-prop-set (name value)
  "Set a file property called NAME to VALUE in buffer file.
  If the property is already set, replace its value."
  (setq name (downcase name))
  (org-with-point-at 1
    (let ((case-fold-search t))
      (if (re-search-forward (concat "^#\\+" name ":\\(.*\\)")
                             (point-max) t)
          (replace-match (concat "#+" name ": " value) 'fixedcase)
        (while (and (not (eobp))
                    (looking-at "^[#:]"))
          (if (save-excursion (end-of-line) (eobp))
              (progn
                (end-of-line)
                (insert "\n"))
            (forward-line)
            (beginning-of-line)))
        (insert "#+" name ": " value "\n")))))

(defun vulpea-buffer-prop-set-list (name values &optional separators)
  "Set a file property called NAME to VALUES in current buffer.
  VALUES are quoted and combined into single string using
  `combine-and-quote-strings'.
  If SEPARATORS is non-nil, it should be a regular expression
  matching text that separates, but is not part of, the substrings.
  If nil it defaults to `split-string-default-separators', normally
  \"[ \f\t\n\r\v]+\", and OMIT-NULLS is forced to t.
  If the property is already set, replace its value."
  (vulpea-buffer-prop-set
   name (combine-and-quote-strings values separators)))

(defun vulpea-buffer-prop-get (name)
  "Get a buffer property called NAME as a string."
  (org-with-point-at 1
    (when (re-search-forward (concat "^#\\+" name ": \\(.*\\)")
                             (point-max) t)
      (buffer-substring-no-properties
       (match-beginning 1)
       (match-end 1)))))

(defun vulpea-buffer-prop-get-list (name &optional separators)
  "Get a buffer property NAME as a list using SEPARATORS.
  If SEPARATORS is non-nil, it should be a regular expression
  matching text that separates, but is not part of, the substrings.
  If nil it defaults to `split-string-default-separators', normally
  \"[ \f\t\n\r\v]+\", and OMIT-NULLS is forced to t."
  (let ((value (vulpea-buffer-prop-get name)))
    (when (and value (not (string-empty-p value)))
      (split-string-and-unquote value separators))))

(defun vulpea-buffer-prop-remove (name)
  "Remove a buffer property called NAME."
  (org-with-point-at 1
    (when (re-search-forward (concat "\\(^#\\+" name ":.*\n?\\)")
                             (point-max) t)
      (replace-match ""))))

(defun vulpea-project-p ()
  "Return non-nil if current buffer has any todo entry.

  TODO entries marked as done are ignored, meaning the this
  function returns nil if current buffer contains only completed
  tasks."
  (seq-find                                 ; (3)
   (lambda (type)
     (eq type 'todo))
   (org-element-map                         ; (2)
       (org-element-parse-buffer 'headline) ; (1)
       'headline
     (lambda (h)
       (org-element-property :todo-type h)))))

(defun vulpea-project-update-tag ()
  "Update PROJECT tag in the current buffer."
  (when (and (not (active-minibuffer-window))
             (vulpea-buffer-p))
    (save-excursion
      (goto-char (point-min))
      (let* ((tags (vulpea-buffer-tags-get))
             (original-tags tags))
        (if (vulpea-project-p)
            (setq tags (cons "project" tags))
          (setq tags (remove "project" tags)))

        ;; cleanup duplicates
        (setq tags (seq-uniq tags))

        ;; update tags if changed
        (when (or (seq-difference tags original-tags)
                  (seq-difference original-tags tags))
          (apply #'vulpea-buffer-tags-set tags))))))
#+end_src
** citar-org-roam
#+begin_src elisp :tangle no
  (setup citar-org-roam
    (:option citar-org-roam-subdir "reference"
             citar-org-roam-note-title-template "${title}")
    (:hook-into org-roam-mode)
    (:when-loaded
      (defun citar-org-roam--create-capture-note (citekey entry)
        "Open or create org-roam node for CITEKEY and ENTRY."
        ;; adapted from https://jethrokuan.github.io/org-roam-guide/#orgc48eb0d
        (let ((title (citar-format--entry
                      citar-org-roam-note-title-template entry)))
          (org-roam-capture-
           :templates
           '(("r" "reference" plain "%?" :if-new
              (file+head "reference/${title}.org"
                         ":PROPERTIES:\n:ROAM_REFS: [cite:@${citekey}]\n:END:\n#+title: ${title}\n* Action notes\n* Idea notes\n* Sealed notes")
              :unnarrowed t))
        :info (list :citekey citekey)
        :node (org-roam-node-create :title title)
        :props '(:finalize find-file))))
  ))
#+end_src
** denote-convert-from-org-roam
#+begin_src elisp :tangle no
(defun org-roam-convert-to-denote (files dir)
  "Convert org-roam notes to denote format and insert them into
`denote-directory'. Works in Dired."
  (interactive
   (list (dired-get-marked-files t current-prefix-arg nil nil t)
         (read-directory-name "Denote directory: " (denote-directory))))
  (unless (file-exists-p dir) (error "Directory does not exist: %s" dir))

  (dolist (file files)
    (let* ((filename (file-name-base file))
           (org-roam-filename-regex
             (rx
              ;; date: YYYY-MM-DD
              (group (= 4 digit) "-" (= 2 digit) "-" (= 2 digit))
              "-"
              ;; title
              (group (* any))))
           (match? (string-match org-roam-filename-regex filename)))
      (unless match?
        (warn "Filename doesn't match org-roam-filename-regex: %s" filename))
      (let* ((date (match-string 1 filename))

             (format-time-string "%H%M%S")
             ;; (time (match-string 2 filename))
             (title (or ;; Try to get title with spaces.
                        (denote-retrieve--value-title file)
                        (replace-regexp-in-string "_" " " (match-string 2 filename))))
             (decoded-time (date-to-time (concat date "T" (format-time-string "%H%M%S"))))
             (id (format-time-string denote--id-format decoded-time))
             ;; Code from `org-roam-tag-add'.
             (keywords (with-temp-buffer (insert-file-contents file nil 0 1024)
                         (split-string (or (cadr (assoc "filetags"
                                                        (org-collect-keywords '("filetags"))))
                                           "")
                                       ":" 'omit-nulls)))
             (keywords (denote--sluggify-keywords
                        (if (cdr keywords) keywords (car keywords))))
             (new-name (denote--format-file
                        (denote-directory)
                        id
                        keywords
                        (denote--sluggify title)
                        (denote--file-extension))))
        ;; Ask for confirmation when overwriting, but don't throw an error if
        ;; the user declines.
        (ignore-errors (copy-file file new-name 1))

        (with-current-buffer (find-file-noselect new-name t)
          (when-let* (((denote--only-note-p file))
                      (filetype (denote-dired--filetype-heuristics file))
                      (date (denote--date (date-to-time id)))
                      (new-front-matter (denote--file-meta-header title date keywords id filetype)))
              (goto-char (point-min))
              (insert new-front-matter)))

        (message "Converted %s -> %s" file new-name))))
  (denote-dired-update-dired-buffers))

#+end_src
